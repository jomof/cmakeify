apply plugin: 'java'

group 'jomofisher'
if (System.getenv()["TRAVIS_TAG"] != null) {
    version System.getenv()["TRAVIS_TAG"]
} else {
    version 'dev'
}

repositories {
  jcenter()
}

buildscript {
  repositories {
      jcenter()
  }
}

class GenerateBuildInfo extends DefaultTask {
    @OutputFile
    public File buildInfoFile = new File("src/main/java/com/jomofisher/cmakeify/BuildInfo.java")

    @TaskAction
    def generate() {
        buildInfoFile.parentFile.mkdirs()
        buildInfoFile.write(
                """package com.jomofisher.cmakeify;
public class BuildInfo {
  static final String PROJECT_VERSION = "$project.version";
}""")
    }
}

task generateBuildInfo(type : GenerateBuildInfo)
compileJava.dependsOn generateBuildInfo
//compileJava.source generateBuildInfo.buildInfoFile


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compile 'org.yaml:snakeyaml:1.17'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'com.google.guava', name: 'guava', version: '20.0'
    testCompile group: 'com.google.truth', name: 'truth', version: '0.31'
    testCompile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'

}
